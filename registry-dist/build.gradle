/*
 * Copyright 2018-2020 Cloudera, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
*/

plugins {
    id 'distribution'
    id 'signing'
}

configurations {
    runtimeJars
}

distributions {
    main {
        baseName = final_name
        contents {
            into('bin/') {
                from("$rootDir/bin")
                fileMode = 0755
            }
            into('bootstrap/') {
                from("$rootDir/bootstrap")
                fileMode = 0755
            }
            into('bootstrap/lib') {
                from("$rootDir/storage/storage-tool/build/libs") {
                    exclude '*javadoc*jar*'
                    exclude '*sources*jar*'
                }
            }
            into('libs/') {
                from(configurations.runtimeJars) {
                    exclude '*shim*'
                    exclude 'commons-logging*jar'
                    exclude 'log4j-api*jar' // log4j 2.*
                    exclude 'log4j-core*jar' // log4j 2.*
                    exclude 'log4j-1*jar' // log4j 1.*
                    exclude 'reload4j*jar'
                }
                from("$rootDir/schema-registry/hadoop-extension/build/libs/hadoop-schema-registry-plugin-impl") {
                    include '*aws-java-sdk-bundle*jar'
                }
            }

            // Ranger plugin
            into('ranger-plugin/ranger-schema-registry-plugin-impl/') {
                from("$rootDir/schema-registry/schema-registry-authorizer/build/libs") {
                    include '*ranger*jar'
                }
                from("$rootDir/ranger-plugin/build/dependency") {
                    include '*jar'
                    exclude 'ranger-plugin-classloader*jar'
                    exclude '*slf4j*jar'
                    exclude '*commons-logging*jar'
                    exclude '*log4j*jar'
                    exclude 'reload4j*jar'
                    exclude '*aws-java-sdk-bundle*jar'
                }
            }
            into('ranger-plugin/conf/') {
                from("$rootDir/schema-registry/schema-registry-authorizer/conf")
            }
            into('ranger-plugin/') {
                from("$rootDir/schema-registry/schema-registry-authorizer/build/libs") {
                    include '*shim*jar'
                }
            }

            // Atlas plugin
            into('atlas-plugin/atlas-schema-registry-plugin-impl/') {
                from("$rootDir/atlas-integration/atlas-translate/build/libs") {
                    exclude '*javadoc*jar'
                    exclude '*sources*jar'
                    exclude '*atlasDeps*jar'
                    exclude '*aws-java-sdk-bundle*jar'
                }
                from("$rootDir/atlas-integration/atlas-translate/build/dependency") {
                    include '*jar'
                    exclude '*slf4j*jar'
                    exclude '*commons-logging*jar'
                    exclude '*log4j*jar'
                    exclude 'reload4j*jar'
                    exclude '*atlas-common*jar'
                    exclude '*aws-java-sdk-bundle*jar'
                }
            }
            into('atlas-plugin/') {
                from("$rootDir/atlas-integration/atlas-shim/build/libs") {
                    exclude '*javadoc*jar'
                    exclude '*sources*jar'
                }
            }

            // Hadoop plugin
            into('hadoop-plugin/hadoop-schema-registry-plugin-impl/') {
                from("$rootDir/schema-registry/hadoop-extension/build/libs") {
                    include '*plugin*jar'
                }
                from("$rootDir/schema-registry/hadoop-extension/build/libs/hadoop-schema-registry-plugin-impl") {
                    include '*jar'
                    exclude '*slf4j*jar'
                    exclude '*commons-logging*jar'
                    exclude '*log4j*jar'
                    exclude '*aws-java-sdk-bundle*jar'
                }
            }
            into('hadoop-plugin/') {
                from("$rootDir/schema-registry/hadoop-extension/build/libs") {
                    include '*shim*jar'
                }
            }

            // examples 
            into('examples/schema-registry/avro') {
                from("$rootDir/examples/schema-registry/avro-examples/build/libs") {
                    exclude '*javadoc*jar'
                    exclude '*sources*jar'
                }
            }
            into('examples/schema-registry/serdes') {
                from("$rootDir/examples/schema-registry/serdes-examples/build/libs") {
                    exclude '*javadoc*jar'
                    exclude '*sources*jar'
                }
            }
            into('examples/schema-registry/avro/data') {
                from("$rootDir/examples/schema-registry/avro-examples/src/main/resources")
            }
            
            into('conf/') {
                from("$rootDir/conf")
            }
            
            // Licenses
            into('.') {
                from(rootDir) {
                    include 'README.md'
                    include 'LICENSE.txt'
                    include 'NOTICE.txt'
                }
            }
            
        }
    }
}

tasks.withType(Tar){
    compression = Compression.GZIP
    archiveExtension = 'tar.gz'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    
    runtimeJars configurations.runtimeClasspath

    compile project(':schema-registry:schema-registry-client')
    compile(project(':schema-registry:schema-registry-webservice')) {
        exclude group: 'mysql'
    }
    compile project(':atlas-integration:atlas-module')
    compile project(':schema-registry:schema-registry-serdes')
    
    compile libraries.jackson.scala_module
    compile(libraries.ranger.classloader) {
        exclude group: 'org.slf4j'
    }
    
    // this will force Gradle to build the tar and zip file only after every other project was built
    distTar.dependsOn(':schema-registry:schema-registry-webservice:build')
    distTar.dependsOn(':registry-common-client:build')
    distTar.dependsOn(':cache:build')
    distTar.dependsOn(':common-auth:build')
    distTar.dependsOn(':ranger-plugin:build')
    distTar.dependsOn(':schema-registry:schema-registry-common:build')
    distTar.dependsOn(':atlas-integration:atlas-common:build')
    distTar.dependsOn(':atlas-integration:atlas-shim:build')
    distTar.dependsOn(':atlas-integration:atlas-translate:build')
    distTar.dependsOn(':storage:storage-common:build')
    distTar.dependsOn(':storage:storage-core:build')
    distTar.dependsOn(':storage:storage-tool:build')
    distTar.dependsOn(':schema-registry:schema-registry-core:build')
    distTar.dependsOn(':schema-registry:schema-registry-authorizer:build')
    distTar.dependsOn(':schema-registry:schema-registry-authorizer:copyDependencies')
    distTar.dependsOn(':schema-registry:hadoop-extension:build')
    distTar.dependsOn(':schema-registry:hadoop-extension:copyDependencies')
    distTar.dependsOn(':examples:schema-registry:serdes-examples:build')
    distTar.dependsOn(':examples:schema-registry:avro-examples:build')
    distTar.dependsOn(':examples:schema-registry:schema-lifecycle:build')
    distTar.dependsOn(':registry-webservice:build')

}

signing {
    sign distTar
    sign distZip
}

//uploadArchives {
//    repositories {
//        mavenDeployer {
//            repository(url: "https://oss.sonatype.org/content/repositories/snapshots")
//        }
//    }
//}

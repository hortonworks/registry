/*
 * Copyright 2018-2021 Cloudera, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
*/

configurations {
    atlasJars
}

dependencies {

    testCompile project(':schema-registry:schema-registry-client')
    testCompile project(':schema-registry:schema-registry-serdes')
    testCompile project(':schema-registry:oauth2-authenticator')
    testCompile project(':schema-registry:schema-registry-webservice')
    testCompile(project(path: ':atlas-integration:atlas-translate', configuration: 'runtime')) {
        exclude group: 'commons-collections'
        exclude group: 'commons-validator'
        exclude group: 'org.apache.hadoop'
        exclude group: 'org.springframework'
        exclude group: 'com.google.guava'
        exclude group: 'com.sun.jersey.contribs'
        exclude group: 'com.sun.jersey'
        exclude group: 'com.fasterxml.jackson.jaxrs'
        exclude group: 'org.slf4j'
        exclude group: 'cglib'
    }
    // Atlas dependencies which will be copied to a separate location, to
    // be loaded by the custom classpathloader
    atlasJars(project(path: ':atlas-integration:atlas-translate', configuration: 'atlasJars')) {
        transitive = false
    }
    // we need this to use Atlas types, but we don't import any transitive dependencies
    testCompile libraries.atlas.intg

    testCompile libraries.flyway
    testCompile libraries.freemarker
    // we need to use version 200 due to new features, but we also need to keep
    // the old version in storage-core because (h2) transactions don't work with newer versions
    testCompile "com.h2database:h2:1.4.200"
    testCompile libraries.httpclient.client
    testCompile libraries.httpclient.mime
    testCompile libraries.validation_api2
    testCompile libraries.nimbus
    testCompile libraries.jose4j

    testCompile libraries.junit
    testCompile libraries.mockito
    testCompile libraries.mockito_junit5
    testCompile(libraries.mockserver) {
        exclude group: "io.netty"
    }

    testCompile libraries.cucumber.core
    testCompile libraries.cucumber.junit
    testCompile libraries.cucumber.junit_platform
    testCompile libraries.cucumber.picocontainer

}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.testCompile + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--plugin', 'html:build/reports/cucumber/cucumber-pretty.html',
                    '--glue', 'com.cloudera.dim.schemaregistry', 'src/test/resources']
        }
    }
}

check.dependsOn(cucumber)

task copyAtlasDependencies(type: Copy) {
    into "$buildDir/atlasJars"
    from configurations.atlasJars
}
testClasses.dependsOn(copyAtlasDependencies)
